dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.52)
 
AC_INIT(file-roller, 2.13.2)
AC_CONFIG_SRCDIR(src/main.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(config.h)

AM_PROG_LIBTOOL

GNOME_DOC_INIT

dnl ==========================================================================
dnl
dnl If you add a version number here, you *must* add an AC_SUBST line for
dnl it too, or it will never make it into the spec file!
dnl
dnl ==========================================================================

GLIB_REQUIRED=2.6.0
GTK_REQUIRED=2.6.0
LIBGNOME_REQUIRED=2.6.0
LIBGNOMEUI_REQUIRED=2.6.0
GNOME_VFS_REQUIRED=2.9.0
LIBGLADE_REQUIRED=2.4.0
NAUTILUS_REQUIRED=2.9.0

AC_SUBST(GLIB_REQUIRED)
AC_SUBST(GTK_REQUIRED)
AC_SUBST(LIBGNOME_REQUIRED)
AC_SUBST(LIBGNOMEUI_REQUIRED)
AC_SUBST(GNOME_VFS_REQUIRED)
AC_SUBST(LIBGLADE_REQUIRED)
AC_SUBST(NAUTILUS_REQUIRED)

dnl ===========================================================================

PKG_CHECK_MODULES(FR,					\
	glib-2.0 >= $GLIB_REQUIRED			\
	gthread-2.0					\
	gtk+-2.0 >= $GTK_REQUIRED			\
	libgnome-2.0 >= $LIBGNOME_REQUIRED		\
	libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED		\
	gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED		\
	gnome-vfs-module-2.0				\
	libglade-2.0 >= $LIBGLADE_REQUIRED)
AC_SUBST(FR_CFLAGS)
AC_SUBST(FR_LIBS)


build_nautilus_actions=no
AC_ARG_ENABLE(nautilus_actions, AC_HELP_STRING([--disable-nautilus-actions],[dont't build the nautilus context menu actions]))

if test x"$enable_nautilus_actions" != xno; then
	if pkg-config --atleast-version=$NAUTILUS_REQUIRED libnautilus-extension; then
		NAUTILUS_CFLAGS=`pkg-config --cflags libnotify glib-2.0`
		NAUTILUS_LIBS=`pkg-config --libs libnotify glib-2.0`
		build_nautilus_actions=yes
	fi
fi
AM_CONDITIONAL(ENABLE_NAUTILUS_ACTIONS, test "x$build_nautilus_actions" = xyes)

AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

dnl Checks for mkdtemp function

mkdtemp_missing=false
AC_CHECK_FUNC(mkdtemp,
    [AC_DEFINE([HAVE_MKDTEMP], 1, [Have GlibC function to make temp dirs])],
    mkdtemp_missing=true)
AM_CONDITIONAL(MKDTEMP_MISSING, test x$mkdtemp_missing = xtrue)

dnl ******************************

ALL_LINGUAS="am ar az be bg bn bs ca cs cy da de el en_CA en_GB es et eu fa fi fr ga gl gu he hi hr hu id is it ja kn ko ku lt lv mk ml mn ms nb ne nl nn no pa pl pt pt_BR ro ru rw sk sl sq sr sr@ije sr@Latn sv ta te th tk tr uk vi xh zh_CN zh_TW"

GETTEXT_PACKAGE=file-roller
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [GetText Package])

AM_GLIB_GNU_GETTEXT
AC_PROG_INTLTOOL

if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(LOCALEDIR, "${ac_default_prefix}/${DATADIRNAME}/locale", [Locale dir])
else
  AC_DEFINE_UNQUOTED(LOCALEDIR, "${prefix}/${DATADIRNAME}/locale", [Locale dir])
fi

dnl ******************************

AC_ARG_ENABLE(deprecations,AC_HELP_STRING([--enable-deprecations],[warn about deprecated usages]))

AM_CONDITIONAL(ENABLE_DEPRECATIONS, test "x$enable_deprecations" = xyes)

dnl ******************************

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
if test x"$GCONFTOOL" = xno; then
	AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi
AM_GCONF_SOURCE_2

dnl ******************************

AC_OUTPUT([
Makefile
file-roller.spec
data/Makefile
data/glade/Makefile
src/Makefile
src/icons/Makefile
src/recent-files/Makefile
nautilus/Makefile
help/Makefile
po/Makefile.in
])

echo "

Configuration:

        Source code location:   ${srcdir}
        Compiler:               ${CC}
        Internal mkdtemp:       ${mkdtemp_missing}
	Nautilus support: 	${build_nautilus_actions}
"
